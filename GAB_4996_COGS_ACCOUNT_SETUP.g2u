Program.Sub.ScreenSU.Start
Gui.F_Setup..Create
Gui.F_Setup..Caption("COGS Account Setup [APAC]")
Gui.F_Setup..Size(6030,6720)
Gui.F_Setup..MinX(0)
Gui.F_Setup..MinY(0)
Gui.F_Setup..Position(0,0)
Gui.F_Setup..BackColor(-2147483633)
Gui.F_Setup..MousePointer(0)
Gui.F_Setup..Event(UnLoad,Unload)
Gui.F_Setup.GsGCSetup.Create(GsGridControl)
Gui.F_Setup.GsGCSetup.Size(5670,6030)
Gui.F_Setup.GsGCSetup.Position(60,60)
Gui.F_Setup.GsGCSetup.Event(RowCellClick,GsGCSetup_RowCellClick)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
V.Local.sIcon.Declare

F.odbc.Connection!conx.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)
F.Intrinsic.String.Build("{0}\ART\gss2.ico",V.Caller.GlobalDir,V.Local.sIcon)
gui.F_Setup..Icon(V.Local.sIcon)

gui.F_Setup.GsGCSetup.Anchor(15)

F.Intrinsic.Control.CallSub(loadscreen)
F.Intrinsic.Control.CallSub(setcontextmenus)

gui.F_Setup..Show
Program.Sub.Main.End

Program.Sub.Unload.Start
F.Data.DataTable.Close("dtCOGS")
F.ODBC.Connection!conx.Close
F.Intrinsic.Control.End

Program.Sub.Unload.End

Program.Sub.LoadScreen.Start
V.Local.iID.Declare
'COGS account
F.Data.DataTable.CreateFromSQL("dtCOGS","conx","select id as ID, gl_account as GL, gl_account as Previous from GAB_4996_COGS_ACCT order by id",True)
'No COGS account has been setup previously
F.Intrinsic.Control.If(V.DataTable.dtCOGS.RowCount,=,0)
	F.Data.DataTable.AddRow("dtCOGS","ID",1,"GL","","Previous","")
'There is a COGS account setup previously
F.Intrinsic.Control.Else
	F.Intrinsic.Math.Add(V.DataTable.dtCOGS(V.DataTable.dtCOGS.RowCount--).ID!FieldVal,1,V.Local.iID)
	F.Data.DataTable.AddRow("dtCOGS","ID",V.Local.iID,"GL","","Previous","")
F.Intrinsic.Control.EndIf

F.Data.DataTable.AddColumn("dtCOGS","Delete","Long",0)

gui.F_Setup.GsGCSetup.AddGridviewFromDatatable("gvCOGS","dtCOGS")
gui.F_Setup.GsGCSetup.MainView("gvCOGS")
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","Previous","Visible",False)
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","ID","HeaderHAlignment","Center")
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","GL","HeaderHAlignment","Center")
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","ID","CellHAlignment","Center")
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","GL","AllowEdit",False)
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","Delete","AllowEdit",False)
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","Delete","ShowCaption",False)
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","ID","MinWidth","60")
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","GL","MinWidth","200")
gui.F_Setup.GsGCSetup.SetColumnProperty("gvCOGS","Delete","MinWidth","100")
gui.F_Setup.GsGCSetup.ColumnEdit("gvCOGS","Delete","EditorButton","Delete")
Program.Sub.LoadScreen.End

Program.Sub.GsGCSetup_RowCellClick.Start
V.Local.iID.Declare
V.Local.iWidths.Declare
V.Local.sCurrentGL.Declare
V.Local.sFilter.Declare
V.Local.sMessage.Declare
V.Local.sResult.Declare
V.Local.sRet.Declare
V.Local.sSQL.Declare
V.Local.sTitles.Declare

F.Intrinsic.Control.If(V.Args.Column,=,"GL")
	V.Local.sCurrentGL.Set(V.DataTable.dtCOGS(V.Args.RowIndex).GL!FieldValTrim)
	'Open a browser to select GL account
	F.Intrinsic.String.Split("GL Account*!*Account Description","*!*",V.Local.sTitles)
	F.Intrinsic.String.Split("1200*!*2000","*!*",V.Local.iWidths)
	
	F.Intrinsic.UI.SetBrowserHotTypeAhead(True)
	F.Intrinsic.UI.Browser("Select a GL account","conx","select gl_account, descr from v_gl_master order by gl_account",V.Local.sTitles,V.Local.iWidths,V.Local.sRet)
	
	F.Intrinsic.Control.If(V.Local.sRet,<>,"***CANCEL***")
		F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
		
		'Check if selected GL account is the same as the current one
		F.Intrinsic.Control.If(V.Local.sRet(0).Trim,<>,V.Local.sCurrentGL.Trim)
			'Check if selected GL account has been selected in the list
			F.Intrinsic.String.Build("GL = '{0}'",V.Local.sRet(0).Trim,V.Local.sFilter)
			F.Data.DataTable.Select("dtCOGS",V.Local.sFilter,V.Local.sResult)
			F.Intrinsic.Control.If(V.Local.sResult,<>,"***NORETURN***")
				F.Intrinsic.String.Build("{0} has been inputted in the list",V.Local.sRet(0).Trim,V.Local.sMessage)
				F.Intrinsic.UI.Msgbox(V.Local.sMessage)
			F.Intrinsic.Control.Else
				F.Data.DataTable.SetValue("dtCOGS",V.Args.RowIndex,"GL",V.Local.sRet(0).Trim)
				F.Intrinsic.Control.If(V.DataTable.dtCOGS(V.Args.RowIndex).Previous!FieldValTrim,=,"")
					F.Intrinsic.String.Build("insert into GAB_4996_COGS_ACCT(id,gl_account) values({0},'{1}');",V.DataTable.dtCOGS(V.DataTable.dtCOGS.RowCount--).ID!FieldVal,V.Local.sRet(0).Trim,V.Local.sSQL)
					F.ODBC.Connection!conx.Execute(V.Local.sSQL)
					F.Intrinsic.Math.Add(V.DataTable.dtCOGS(V.DataTable.dtCOGS.RowCount--).ID!FieldVal,1,V.Local.iID)
					F.Data.DataTable.AddRow("dtCOGS","ID",V.Local.iID,"GL","","Previous","")
				F.Intrinsic.Control.Else
					F.Intrinsic.String.Build("update GAB_4996_COGS_ACCT set gl_account = '{1}' where id = {0};",V.DataTable.dtCOGS(V.Args.RowIndex).ID!FieldVal,V.Local.sRet(0).Trim,V.Local.sSQL)
					F.ODBC.Connection!conx.Execute(V.Local.sSQL)
				F.Intrinsic.Control.EndIf
				F.Data.DataTable.SetValue("dtCOGS",V.Args.RowIndex,"Previous",V.Local.sRet(0).Trim)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.ElseIf(V.Args.Column,=,"Delete")
	F.Intrinsic.Control.If(V.DataTable.dtCOGS(V.Args.RowIndex).Previous!FieldValTrim,<>,"")
		F.Intrinsic.String.Build("delete from GAB_4996_COGS_ACCT where id = {0}",V.DataTable.dtCOGS(V.Args.RowIndex).ID!FieldValLong,V.Local.sSQL)
		F.ODBC.Connection!conx.Execute(V.Local.sSQL)
		F.Data.DataTable.DeleteRow("dtCOGS",V.Args.RowIndex)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
Program.Sub.GsGCSetup_RowCellClick.End

Program.Sub.SetContextMenus.Start
Gui.F_Setup..ContextMenuCreate("CTXCOGS")
Gui.F_Setup.GsGCSetup.ContextMenuAttach("CTXCOGS")
Gui.F_Setup..ContextMenuAddItem("CTXCOGS","Import",0,"Import from Excel")
Gui.F_Setup..ContextMenuSetItemEventHandler("CTXCOGS","Import","ContextMenuImport")
Program.Sub.SetContextMenus.End

Program.Sub.ContextMenuImport.Start
V.Local.bExist.Declare
V.Local.i1.Declare
V.Local.iID.Declare
V.Local.iRow.Declare
V.Local.iTRow.Declare
V.Local.sControl.Declare
V.Local.sFilename.Declare
V.Local.sFilter.Declare
V.Local.sMessage.Declare
V.Local.sResult.Declare
V.Local.sRet.Declare
V.Local.sRow.Declare
V.Local.sSQL.Declare

'Get the excel upload file
F.Intrinsic.UI.ShowOpenFileDialog("","*.xls|","",V.Local.sFileName)

F.Intrinsic.Control.If(V.Local.sFileName,<>,"***CANCEL***")
	F.Automation.MSExcel.CreateAppObject("MSExcel")
	F.Automation.MSExcel.OpenWorkbook("MSExcel","UPL",V.Local.sFileName)
	
	F.Automation.MSExcel.OpenWorksheet("UPL","UPLSheet",1)
	F.Automation.MSExcel.RowCount("UPLSheet",V.Local.iTRow)
	
	F.Intrinsic.UI.InvokeWaitDialog("Uploading COGS accounts from Excel","COGS Account Setup [APAC]")
	F.Intrinsic.Control.For(V.Local.iRow,2,V.Local.iTRow,1)
		F.Intrinsic.Math.Sub(V.Local.iRow,1,V.Local.i1)
		F.Intrinsic.String.Build("Processing row {0}",V.Local.i1,V.Local.sMessage)
		F.Intrinsic.UI.ChangeWaitStatus(V.Local.sMessage,V.Local.iRow,1,V.Local.iTRow)
		F.Automation.MSExcel.ReadRow("UPLSheet",V.Local.iRow,1,2,V.Local.sRow)
		F.Intrinsic.String.Split(V.Local.sRow,"*!*",V.Local.sRow)
		
		F.Intrinsic.Control.If(V.Local.sRow(0).Trim,<>,"")
			F.Intrinsic.String.Build("GL = '{0}'",V.Local.sRet(0).Trim,V.Local.sFilter)
			F.Data.DataTable.Select("dtCOGS",V.Local.sFilter,V.Local.sResult)
			F.Intrinsic.Control.If(V.Local.sResult,=,"***NORETURN***")
				F.Intrinsic.Math.Add(V.DataTable.dtCOGS(V.DataTable.dtCOGS.RowCount--).ID!FieldVal,1,V.Local.iID)
				F.Data.DataTable.SetValue("dtCOGS",V.DataTable.dtCOGS(V.DataTable.dtCOGS.RowCount--).ID!FieldVal,"GL",V.Local.sRow(0).Trim,"Previous",V.Local.sRow(0).Trim)
				F.Intrinsic.String.Build("insert into GAB_4996_COGS_ACCT(id,gl_account) values({0},'{1}');",V.DataTable.dtCOGS(V.DataTable.dtCOGS.RowCount--).ID!FieldVal,V.Local.sRow(0).Trim,V.Local.sSQL)
				F.ODBC.Connection!conx.Execute(V.Local.sSQL)
				F.Data.DataTable.AddRow("dtCOGS","ID",V.Local.iID,"GL","","Previous","")
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Next(V.Local.iRow)
	F.Automation.MSExcel.DestroyAllObjects("MSExcel","UPL","UPLSheet")
F.Intrinsic.Control.EndIf
Program.Sub.ContextMenuImport.End

Program.Sub.Comments.Start
${$0$}$COGS Account Setup for GL JE Import$}$MHERTANTO$}$1/3/2018 2:21:29 PM$}$True
Program.Sub.Comments.End